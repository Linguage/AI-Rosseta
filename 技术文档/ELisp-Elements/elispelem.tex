% Created 2025-04-14 Mon 10:08
% Intended LaTeX compiler: xelatex
\documentclass[11pt]{ctexart}
\usepackage[UTF8]{ctex}
\usepackage{amsmath}
\usepackage{amssymb}
\usepackage{graphicx}
\usepackage{hyperref}
% 设置页边距
\usepackage[top=2.5cm, bottom=2.5cm, left=2cm, right=2cm]{geometry}
% 设置行间距
\linespread{1.15}
% 设置段落间距
\setlength{\parskip}{0.5em}
% 设置章节标题与正文间距
\usepackage{titlesec}
\titlespacing*{\section}{0pt}{1.5em}{1em}
\titlespacing*{\subsection}{0pt}{1.25em}{0.75em}
\titlespacing*{\subsubsection}{0pt}{1em}{0.5em}
\usepackage{graphicx}
\usepackage{longtable}
\usepackage{wrapfig}
\usepackage{rotating}
\usepackage[normalem]{ulem}
\usepackage{capt-of}
\usepackage{hyperref}
\author{Protesilaos Stavrou\thanks{info@protesilaos.com}}
\date{\today}
\title{Emacs Lisp Elements}
\hypersetup{
 pdfauthor={Protesilaos Stavrou},
 pdftitle={Emacs Lisp Elements},
 pdfkeywords={},
 pdfsubject={},
 pdfcreator={Emacs 30.1 (Org mode 9.7.11)}, 
 pdflang={English}}
\begin{document}

\maketitle
This book, written by Protesilaos Stavrou, also known as ``Prot'', provides a big picture view of the Emacs Lisp programming language.

The information furnished herein corresponds to stable version 1.0.0, released on 2025-04-12.

\begin{itemize}
\item Official page: \url{https://protesilaos.com/emacs/emacs-lisp-elements}
\item Git repository: \url{https://github.com/protesilaos/emacs-lisp-elements}
\end{itemize}

\setcounter{tocdepth}{8}
\tableofcontents
\section{Getting started with Emacs Lisp}
\label{sec:orga5d8310}
The purpose of this book is to provide you with a big picture view of Emacs Lisp, also known as ``Elisp''. This is the programming language you use to extend Emacs. Emacs is a programmable text editor: it interprets Emacs Lisp and behaves accordingly. You can use Emacs without ever writing a single line of code: it already has lots of features. Though you can, at any time, program it to do exactly what you want by evaluating some Elisp that either you wrote yourself or got from another person, such as in the form of a package.

Programming your own text editor is both useful and fun. You can, for example, streamline a sequence of actions you keep doing by combining them in a single command that you then assign to a key binding: type the key and---bam!---perform all the intermediate tasks in one go. This makes you more efficient while it turns the editor into a comfortable working environment.

The fun part is how you go about writing the code. There are no duties you have to conform with. None! You program for the sake of programming. It is a recreational activity that expands your horizons. Plus, you cultivate your Elisp skills, which can prove helpful in the future, should you choose to modify some behaviour of Emacs.

Tinkering with Emacs is part of the experience. It teaches you to be unapologetically opinionated about how your editor works. The key is to know enough Elisp so that you do not spend too much time having fun or getting frustrated because something trivial does not work. I am writing this as a tinkerer myself with no background in computer science or neighbouring studies: I learnt Emacs Lisp through trial and error by playing around with the editor. My nominal goal was to improve certain micro-motions I was repeating over and over: I sought efficiency only to discover something much more profound. Learning to extend my editor has been a fulfilling experience and I am more productive as a result. Emacs does what I want it to do and I am happy with it.

Each chapter herein is generally short and to-the-point. Some are more friendly to beginners while others dive deeper into advanced topics. There are links between the chapters, exactly how a reference manual is supposed to be done. You may then go back and forth to find what you need.

The text you will find here is a combination of prose and code. The latter may be actual Elisp or pseudo-code which captures the underlying pattern. I encourage you to read this book either inside of Emacs or with Emacs readily available. This way, you can play around with the functions I give you, to further appreciate their nuances.

The ``big picture view'' approach I am adopting is about covering the concepts that I encounter frequently while working with Emacs Lisp. This book is no substitute for the Emacs Lisp Reference Manual and should by no means be treated as the source of truth for any of the Elisp forms I comment on.

Good luck and enjoy!
\section{Evaluate Emacs Lisp}
\label{sec:org91f7fd8}
Everything you do in Emacs calls some function. It evaluates Emacs Lisp code, reading the return values and producing side effects (\hyperref[sec:org2689823]{Side effect and return value}).

You type a key on your keyboard and a character is written to the current buffer. That is a function bound to a key. It actually is an \emph{interactive} function, because you are calling it via a key binding rather than through some program. Interactive functions are known as ``commands''. Though do not let the implementation detail of interactivity distract you from the fact that every single action you perform in Emacs involves the evaluation of Emacs Lisp.

Another common pattern of interaction is with the M-x (\texttt{execute-extended-command}) key, which by default runs the command \texttt{execute-extended-command}: it produces a minibuffer prompt that asks you to select a command by its name and proceeds to execute it.

Emacs can evaluate Elisp code from anywhere. If you have some Elisp in your buffer, you can place the cursor at the end of its closing parenthesis and type C-x C-e (\texttt{eval-last-sexp}). Similarly, you can use the commands \texttt{eval-buffer} and \texttt{eval-region} to operate on the current buffer or highlighted region, respectively.

The \texttt{eval-last-sexp} also works on symbols (\hyperref[sec:org9b7817a]{Symbols, balanced expressions, and quoting}). For example, if you place the cursor at the end of the variable \texttt{buffer-file-name} and use C-x C-e (\texttt{eval-last-sexp}), you will get the value of that variable, which is either \texttt{nil} or the file system path to the file you are editing.

Sometimes the above are not appropriate for what you are trying to do. Suppose you intend to write a command that copies the file path of the current buffer. To do that, you need your code to test the value of the variable \texttt{buffer-file-name} (\hyperref[sec:org426ce04]{Buffers as data structures}). But you do not want to type out \texttt{buffer-file-name} in your actual file, then use one of the aforementioned commands for Elisp evaluation, and then undo your edits. That is cumbersome and prone to mistakes! The best way to run Elisp in the current buffer is to type M-: (\texttt{eval-expression}): it opens the minibuffer and expects you to write the code you want to evaluate. Type RET from there to proceed. The evaluation is done with the last buffer as current (the buffer that was current prior to calling \texttt{eval-expression}).

Here is some Emacs Lisp you may want to try in (i) a buffer that corresponds to a file versus (ii) a buffer that is not associated with any file on disk.

\begin{verbatim}
;; Use `eval-expression' to evaluate this code in a file-visiting
;; buffer versus a buffer that does not visit any file.
(if buffer-file-name
    (message "The path to this file is `%s'" buffer-file-name)
  (message "Sorry mate, this buffer is not visiting a file"))
\end{verbatim}

When you are experimenting with code, you want to test how it behaves. Use the command \texttt{ielm} to open an interactive shell. It puts you at a prompt where you can type any Elisp and hit RET to evaluate it. The return value is printed right below. Alternatively, switch to the \texttt{*scratch*} buffer. If it is using the major mode \texttt{lisp-interaction-mode}, which is the default value of the variable \texttt{initial-major-mode}, then you can move around freely in that buffer and type C-j (\texttt{eval-print-last-sexp}) at the end of some code to evaluate it. This works almost the same way as \texttt{eval-last-sexp}, with the added effect of putting the return value right below the expression you just evaluated.

In addition to these, you can rely on the self-documenting nature of Emacs to figure out what the current state is. For example, to learn about the buffer-local value of the variable \texttt{major-mode}, you can do C-h v (\texttt{describe-variable}), and then search for that variable. The resulting Help buffer will inform you about the current value of \texttt{major-mode}. This help command and many others like \texttt{describe-function}, \texttt{describe-keymap}, \texttt{describe-key}, and \texttt{describe-symbol}, provide insight into what Emacs knows about a given object. The Help buffer will show relevant information, such as the path to the file that defines the given function or whether a variable is declared as buffer-local.

Emacs is ``self-documenting'' because it reports on its state. You do not need to explicitly update the Help buffers. This happens automatically by virtue of evaluating the relevant code: Emacs effectively shows you the latest value of whatever it is you are working with.
\section{Side effect and return value}
\label{sec:org2689823}
Emacs Lisp has functions. They take inputs and produce outputs. In its purest form, a function is a computation that only returns a value: it does not change anything in its environment. The return value of a function is used as input for another function, in what effectively is a chain of computations. You can thus rely on a function's return value to express something like ``if this works, then also do this other thing, otherwise do something else or even nothing.''

Elisp is the language that extends and controls Emacs. This means that it also affects the state of the editor. When you run a function, it can make permanent changes, such as to insert some text at the point of the cursor, delete a buffer, create a new window, and so on. These changes will have an impact on future function calls. For example, if the previous function deleted a certain buffer, the next function which was supposed to write to that same buffer can no longer do its job: the buffer is gone!

When you write Elisp, you have to account for both the return value and the side effects. If you are sloppy, you will get unintended results caused by all those ill-considered changes to the environment. But if you use side effects meticulously, you are empowered to take Elisp to its full potential. For instance, imagine you define a function that follows the logic of ``create a buffer, go there, write some text, save the buffer to a file at my preferred location, and then come back where I was before I called this function, while leaving the created buffer open.'' All these are side effects and they are all useful. Your function may have some meaningful return value as well that you can employ as the input of another function. For example, your function would return the buffer object it generated, so that the next function can do something there like display that buffer in a separate frame and make its text larger.

The idea is to manipulate the state of the editor, to make Emacs do what you envision. Sometimes this means your code has side effects. At other times, side effects are useless or even run counter to your intended results. You will keep refining your intuition about what needs to be done as you gain more experience and expand the array of your skills (\hyperref[sec:org9b7817a]{Symbols, balanced expressions, and quoting}). No problem; no stress!
\section{Buffers as data structures}
\label{sec:org426ce04}
A buffer holds data as a sequence of characters. For example, this data is the text you are looking at when you open a file. Each character exists at a given position, which is a number. The function \texttt{point} gives you the position at the point you are on, which typically corresponds to where the cursor is (\hyperref[sec:org91f7fd8]{Evaluate Emacs Lisp}). At the beginning of a buffer, \texttt{point} returns the value of \texttt{1} (\hyperref[sec:org2689823]{Side effect and return value}). There are plenty of functions that return a buffer position, such as \texttt{point-min}, \texttt{point-max}, \texttt{line-beginning-position}, and \texttt{re-search-forward}. Some of those will have side effects, like \texttt{re-search-forward} which moves the cursor to the given match.

When you program in Emacs Lisp, you frequently rely on buffers to do some of the following:

\begin{description}
\item[{Extract file contents as a string}] Think of the buffer as a large string. You can get the entirety of its contents as one potentially massive string by using the function \texttt{buffer-string}. You may also get a substring between two buffer positions, such as with the \texttt{buffer-substring} function or its \texttt{buffer-substring-no-properties} counterpart (\hyperref[sec:orgcf4e6e5]{Text has its own properties}). Imagine you do this as part of a wider operation that (i) opens a file, (ii) goes to a certain position, (iii) copies the text it found, (iv) switches to another buffer, and (v) writes what it found to this new buffer.
\end{description}

\begin{description}
\item[{Present the results of some operation}] You may have a function that shows upcoming holidays. Your code does the computations behind the scenes and ultimately writes some text to a buffer. The end product is on display. Depending on how you go about it, you will want to evaluate the function \texttt{get-buffer-create} or its more strict \texttt{get-buffer} alternative. If you need to clear the contents of an existing buffer, you might use the \texttt{with-current-buffer} macro to temporarily switch to the buffer you are targetting and then either call the function \texttt{erase-buffer} to delete everything or limit the deletion to the range betweeen two buffer positions with \texttt{delete-region}. Finally, the functions \texttt{display-buffer} or \texttt{pop-to-buffer} will place the buffer in an Emacs window.
\end{description}

\begin{description}
\item[{Associate variables with a given buffer}] In Emacs Lisp, variables can take a buffer-local value which differs from its global counterpart. Some variables are even declared to always be buffer-local, such as the \texttt{buffer-file-name}, \texttt{fill-column}, and \texttt{default-directory}. Suppose you are doing something like returning a list of buffers that visit files in a given directory. You would iterate through the return value of the \texttt{buffer-list} function to filter the results accordingly by testing for a certain value of \texttt{buffer-file-name} (\hyperref[sec:org9074f8f]{Basic control flow with \texttt{if}, \texttt{cond}, and others}). This specific variable is always available, though you can always use the \texttt{setq-local} macro to assign a value to a variable in the current buffer.
\end{description}

The latter point is perhaps the most open-ended one. Buffers are like a bundle of variables, which includes their contents, the major mode they are running, and all the buffer-local values they have. In the following code block, I am using the \texttt{seq-filter} function to iterate through the return value of the function \texttt{buffer-list} (\hyperref[sec:org9b7817a]{Symbols, balanced expressions, and quoting}).

\begin{verbatim}
(seq-filter
 (lambda (buffer)
   "Return BUFFER if it is visible and its major mode derives from `text-mode'."
   (with-current-buffer buffer
     ;; The convention for buffers which are not meant to be seen by
     ;; the user is to start their name with an empty space.  We are
     ;; not interested in those right now.
     (and (not (string-prefix-p " " (buffer-name buffer)))
          (derived-mode-p 'text-mode))))
 (buffer-list))
\end{verbatim}

This will return a list of buffer objects that pass the test of (i) being ``visible'' to the user and (ii) their major mode is either \texttt{text-mode} or derived therefrom. The above may also be written thus (\hyperref[sec:org1669468]{When to use a named function or a lambda function}):

\begin{verbatim}
(defun my-buffer-visble-and-text-p (buffer)
  "Return BUFFER if it is visible and its major mode derives from `text-mode'."
  (with-current-buffer buffer
    ;; The convention for buffers which are not meant to be seen by
    ;; the user is to start their name with an empty space.  We are
    ;; not interested in those right now.
    (and (not (string-prefix-p " " (buffer-name buffer)))
         (derived-mode-p 'text-mode))))

(seq-filter #'my-buffer-visble-and-text-p (buffer-list))
\end{verbatim}

As with buffers, Emacs windows and frames have their own parameters. I will not cover those as their utility is more specialised and the concepts are the same. Just know that they are data structures that you may use to your advantage, including by iterating through them (\hyperref[sec:org29370c2]{Mapping through a list of elements}).
\section{Text has its own properties}
\label{sec:orgcf4e6e5}
Just as with buffers that work like data structures (\hyperref[sec:org426ce04]{Buffers as data structures}), any text may also have properties associated with it. This is metadata that you inspect using Emacs Lisp. For example, when you see syntax highlighting in some programming buffer, this is the effect of text properties. Some function takes care to ``propertise'' or to ``fontify'' the relevant text and decides to apply to it an object known as ``face''. Faces are constructs that bundle together typographic and colour attributes, such as the font family and weight, as well as foreground and background hues. To get a Help buffer with information about the text properties at the point of the cursor, type M-x (\texttt{execute-extended-command}) and then invoke the command \texttt{describe-char}. It will tell you about the character it sees, what font it is rendered with, which code point it is, and what its text properties are.

Suppose you are writings your own major mode. At the early stage of experimentation, you want to manually add text properties to all instances of the phrase \texttt{I have properties} in a buffer whose major mode is \texttt{fundamental-mode}, so you do something like this (\hyperref[sec:orge641c75]{The match data of the last search}):

\begin{verbatim}
(defun my-add-properties ()
  "Add properties to the text \"I have properties\" across the current buffer."
  (goto-char (point-min))
  (while (re-search-forward "I have properties" nil t)
    (add-text-properties (match-beginning 0) (match-end 0) '(face error))))
\end{verbatim}

Actually try this. Use C-x b (\texttt{switch-to-buffer}), type in some random characters that do not match an existing buffer, and then hit RET to visit that new buffer. It runs \texttt{fundamental-mode}, meaning that there is no ``fontification'' happening and, thus, \texttt{my-add-properties} will work as intented. Now paste the following:

\begin{verbatim}
This is some sample text. Will the phrase "I have properties" use the `bold' face?

What does it even mean for I have properties to be bold?
\end{verbatim}

Continue with M-: (\texttt{eval-expression}) and call the function \texttt{my-add-properties}. Did it work? The face it is applying is called \texttt{error}. Ignore the semantics of that word: I picked it simply because it typically is styled in a fairly intense and obvious way (though your current theme may do things differently).

There are functions which find the properties at a given buffer position and others which can search forward and backward for a given property. The specifics do not matter right now. All I want you to remember is that the text can be more than just its constituent characters. For more details, type M-x (\texttt{execute-extended-command}) to call the command \texttt{shortdoc}. It will ask you for a documentation group. Pick \texttt{text-properties} to learn more. Well, use \texttt{shortdoc} for everything listed there. I do it all the time.
\section{Symbols, balanced expressions, and quoting}
\label{sec:org9b7817a}
To someone not familiar with Emacs Lisp, it is a language that has so many parentheses! Here is a simple function definition:

\begin{verbatim}
(defun my-greet-person (name)
  "Say hello to the person with NAME."
  (message "Hello %s" name))
\end{verbatim}

I just defined the function with the name \texttt{my-greet-person}. It has a list of parameters, specifically, a list of one parameter, called \texttt{name}. Then is the optional documentation string, which is for users to make sense of the code and/or understand the intent of the function. \texttt{my-greet-person} takes \texttt{name} and passes it to the function \texttt{message} as an argument to ultimately print a greeting. The \texttt{message} function logs the text in the \texttt{*Messages*} buffer, which you can visit directly with C-h e (\texttt{view-echo-area-messages}). At any rate, this is how you call \texttt{my-greet-person} with the one argument it expects:

\begin{verbatim}
(my-greet-person "Protesilaos")
\end{verbatim}

Now do the same with more than one parameters:

\begin{verbatim}
(defun my-greet-person-from-country (name country)
  "Say hello to the person with NAME who lives in COUNTRY."
  (message "Hello %s of %s" name country))
\end{verbatim}

And call it thus:

\begin{verbatim}
(my-greet-person-from-country "Protesilaos" "Cyprus")
\end{verbatim}

Even for the most basic tasks, you have lots of parentheses. But fear not! These actually make it simpler to have a structural understanding of your code. If it does not feel this way right now, it is because you are not used to it yet. Once you do, there is no going back.

The basic idea of any dialect of Lisp, Emacs Lisp being one of them, is that you have parentheses which delimit lists. A list consists of elements. Lists are either evaluated to produce the results of some computation or returned as they are for use in some other evaluation (\hyperref[sec:org2689823]{Side effect and return value}):

\begin{description}
\item[{The list as a function call}] When a list is evaluated, the first element is the name of the function and the remaining elements are the arguments passed to it. You already saw this play out above with how I called \texttt{my-greet-person} with \texttt{"Protesilaos"} as its argument. Same principle for \texttt{my-greet-person-from-country}, with \texttt{"Protesilaos"} and \texttt{"Cyprus"} as its arguments.

\item[{The list as data}] When a list is not evaluated, then none of its elements has any special meaning at the outset. They are all returned as a list without further changes. When you do not want your list to be evaluated, you prefix it with a single quote character. For example, \texttt{'("Protesilaos" "Prot" "Cyprus")} is a list of three elements that should be returned as-is.
\end{description}

Consider the latter case, which you have not seen yet. You have a list of elements and you want to get some data out of it. At the most basic level, the functions \texttt{car} and \texttt{cdr} return the first element and the list of all remaining elements, respectively:

\begin{verbatim}
(car '("Protesilaos" "Prot" "Cyprus"))
;; => "Protesilaos"

(cdr '("Protesilaos" "Prot" "Cyprus"))
;; => ("Prot" "Cyprus")
\end{verbatim}

The single quote here is critical, because it instructs Emacs to not evaluate the list. Otherwise, the evaluation of this list would treat the first element, namely \texttt{"Protesilaos"}, as the name of a function and the remainder of the list as the arguments to that function. As you do not have the definition of such a function, you get an error.

Certain data types in Emacs Lisp are ``self-evaluating''. This means that if you evaluate them, their return value is what you are already seeing. For example, the return value of the string of characters \texttt{"Protesilaos"} is \texttt{"Protesilaos"}. This is true for strings, numbers, keywords, symbols, and the special \texttt{nil} or \texttt{t}. Here is a list with a sample of each of these, which you construct by calling the function \texttt{list}:

\begin{verbatim}
(list "Protesilaos" 1 :hello 'my-greet-person-from-country nil t)
;; => ("Protesilaos" 1 :hello 'my-greet-person-from-country nil t)
\end{verbatim}

The \texttt{list} function evaluates the arguments passed to it, unless they are quoted. The reason you get the return value without any apparent changes is because of self-evaluation. Notice that \texttt{my-greet-person-from-country} is quoted the same way we quote a list we do not want to evaluate. Without it, \texttt{my-greet-person-from-country} would be evaluated, which would return an error unless that was also defined as a variable.

Think of the single quote as an unambiguous instruction: ``do not evaluate the following.'' More specifically, it is an instruction to not perform evaluation if it would have normally happened in that context (\hyperref[sec:org771b57a]{Partial evaluation inside of a list}). In other words, you do not want to quote something inside of a quoted list, because that is the same as quoting it twice:

\begin{verbatim}
;; This is the correct way:
'(1 :hello my-greet-person-from-country)

;; It is wrong to quote `my-greet-person-from-country' because the
;; entire list would not have been evaluated anyway.  The mistake here
;; is that you are quoting what is already quoted, like doing
;; ''my-greet-person-from-country.
'(1 :hello 'my-greet-person-from-country)
\end{verbatim}

Now you may be wondering why did we quote \texttt{my-greet-person-from-country} but nothing else? The reason is that everything else you saw there is effectively ``self-quoting'', i.e. the flip-side of self-evaluation. Whereas \texttt{my-greet-person-from-country} is a symbol. A ``symbol'' is a reference to something other than itself: it either represents some computation---a function---or the value of a variable. If you write a symbol without quoting it, you are effectively telling Emacs ``give me the value this symbol represents.'' In the case of \texttt{my-greet-person-from-country}, you will get an error if you try that because this symbol is not a variable and thus trying to get a value out of it is not going to work.

Keep in mind that Emacs Lisp has a concept of ``macro'', which basically is a templating system to write code that actually expands into some other code which is then evaluated. Inside of a macro, you control how quoting is done, meaning that the aforementioned may not apply to calls that involve the macro, even if they are still used inside of the macro's expanded form (\hyperref[sec:org61e1a1b]{Evaluation inside of a macro or special form}).

As you expose yourself to more Emacs Lisp code, you will encounter quotes that are preceded by the hash sign, like \texttt{\#'some-symbol}. This ``sharp quote'', as it is called, is the same as the regular quote with the added semantics of referring to a function in particular. The programmer can thus better articulate the intent of a given expression, while the byte compiler may internally perform the requisite checks and optimisations. In this light, read about the functions \texttt{quote} and \texttt{function} which correspond to the quote and sharp quote, respectively.
\section{Partial evaluation inside of a list}
\label{sec:org771b57a}
You already have an idea of how Emacs Lisp code looks like (\hyperref[sec:org9b7817a]{Symbols, balanced expressions, and quoting}). You have a list that is either evaluated or taken as-is. There is another case where a list should be partially evaluated or, more specifically, where it should be treated as data instead of a function call with some elements inside of it still subject to evaluation.

In the following code block, I am defining a variable called \texttt{my-greeting-in-greek}, which is a common phrase in Greek that literally means ``health to you'' and is pronounced as ``yah sou''. Why Greek? Well, you got the \texttt{lambda} that engendered this whole business with Lisp, so you might as well get all the rest (\hyperref[sec:org1669468]{When to use a named function or a lambda function})!

\begin{verbatim}
(defvar my-greeting-in-greek "Γεια σου"
  "Basic greeting in Greek to wish health to somebody.")
\end{verbatim}

Now I want to experiment with the \texttt{message} function to better understand how evaluation works. Let me start with the scenario of quoting the list, thus taking it as-is:

\begin{verbatim}
(message "%S" '(one two my-greeting-in-greek four))
;;=> "(one two my-greeting-in-greek four)"
\end{verbatim}

You will notice that the variable \texttt{my-greeting-in-greek} is not evaluated. I get the symbol, the actual \texttt{my-greeting-in-greek}, but not the value it represents. This is the expected result, because the entire list is quoted and, ipso facto, everything inside of it is not evaluated.

Now check the next code block to understand how I can tell Emacs that I want the entire list to still be quoted but for \texttt{my-greeting-in-greek} in particular to be evaluated, so it is replaced by its value:

\begin{verbatim}
(message "%S" `(one two ,my-greeting-in-greek four))
;; => "(one two \"Γεια σου\" four)"
\end{verbatim}

Pay close attention to the syntax here. Instead of a single quote, I am using the backtick or back quote, which is also known as a ``quasi quote'' in our case. This behaves like the single quote except for anything that is preceded by a comma. The comma is an instruction to ``evaluate the thing that follows'' and only works inside of a quasi-quoted list. The ``thing'' that follows is either a symbol or a list. The list can, of course, be a function call. Let me then use \texttt{concat} to greet a certain person all while returning everything as a list:

\begin{verbatim}
(message "%S" `(one two ,(concat my-greeting-in-greek " " "Πρωτεσίλαε") four))
;; => "(one two \"Γεια σου Πρωτεσίλαε\" four)"
\end{verbatim}

Bear in mind that you would get an error if you were not quoting this list at all, because the first element \texttt{one} would be treated as the symbol a function, which would be called with all other elements as its arguments. Chances are that \texttt{one} is not defined as a function in your current Emacs session or those arguments are not meaningful to it, anyway. Plus, \texttt{two} and \texttt{four} would then be treated as variables, since they are not quoted, in which case those would have to be defined as well, else more errors would ensue.

Other than the comma operator, there is the \texttt{,@} (how is this even pronounced? ``comma at'', perhaps?), which is notation for ``splicing''. This is jargon in lieu of saying ``the return value is a list and I want you to remove the outermost parentheses of it.'' In effect, the code that would normally return \texttt{'(one two three)} now returns \texttt{one two three}. This difference may not make much sense in a vacuum, though it does once you consider those elements as expressions that should work in their own right, rather than simply be elements of a quoted list. I will not elaborate on an example here, as I think this is best covered in the context of defining macros (\hyperref[sec:org61e1a1b]{Evaluation inside of a macro or special form}).

Chances are you will not need to use the knowledge of partial evaluation. It is more common in macros, though can be applied anywhere. Be aware of it regardless, as there are scenaria where you will, at the very least, want to understand what some code you depend on is doing.

Lastly, since I introduced you to some Greek words, I am now considering you my friend. Here is a joke from when I was a kid. I was trying to explain some event to my English instructor. As I lacked the vocabulary to express myself, I started using Greek words. My instructor had a strict policy of only responding to English, so she said ``It is all Greek to me.'' Not knowing that her answer is an idiom for ``I do not understand you'', I blithely replied, ``Yes, Greek madame; me no speak England very best.'' I was not actually a beginner at the time, though I would not pass on the opportunity to make fun of the situation. Just how you should remember to enjoy the time spent tinkering with Emacs. But enough of that! Back to reading this book.
\section{Evaluation inside of a macro or special form}
\label{sec:org61e1a1b}
In the most basic case of Emacs Lisp code, you have lists that are either evaluated or not (\hyperref[sec:org9b7817a]{Symbols, balanced expressions, and quoting}). If you get a little more fancy, you have lists that are only partially evaluated (\hyperref[sec:org771b57a]{Partial evaluation inside of a list}). Sometimes though, you look at a piece of code and cannot understand why the normal rules of quoting and evaluation do not apply. Before you see this in action, inspect a typical function call that also involves the evaluation of a variable:

\begin{verbatim}
(concat my-greeting-in-greek " " "Πρωτεσίλαε")
\end{verbatim}

You encountered this code in the section about partial evaluation. What you have here is a call to the function \texttt{concat}, followed by three arguments. One of these arguments is a variable, the \texttt{my-greeting-in-greek}. When this list is evaluated, what Emacs actually does is to first evaluate the arguments, including \texttt{my-greeting-in-greek}, in order to get their respective values and only then to call \texttt{concat} with those values. You can think of the entire operation as follows:

\begin{itemize}
\item Here is a list.
\item It is not quoted.
\item So you should evaluate it.
\item The first element is the name of the function.
\item The remaining elements are arguments passed to that function.
\item Check what the arguments are.
\item Evaluate each of the arguments to resolve it to its actual value.
\item Strings are self-evaluating, while the \texttt{my-greeting-in-greek} is a variable.
\item You now have the value of each of the arguments, including the value of the symbol \texttt{my-greeting-in-greek}.
\item Call \texttt{concat} with all the values you got.
\end{itemize}

In other words, the following two yield the same results (assuming a constant \texttt{my-greeting-in-greek}):

\begin{verbatim}
(concat my-greeting-in-greek " " "Πρωτεσίλαε")

(concat "Γεια σου" " " "Πρωτεσίλαε")
\end{verbatim}

This is predictable. It follows the basic logic of the single quote: if it is quoted, do not evaluate it and return it as-is, otherwise evaluate it and return its value. But you will find plenty of cases where this expected pattern is seemingly not followed. Consider this common case of using \texttt{setq} to bind a symbol to the given value:

\begin{verbatim}
(setq my-test-symbol "Protesilaos of Cyprus")
\end{verbatim}

The above expression looks like a function call, meaning that (i) the list is not quoted, (ii) the first element is the name of a function, and (iii) the remaining elements are arguments passed to that function. In a way, this is all true. Though you would then expect the \texttt{my-test-symbol} to be treated as a variable, which would be evaluated in place to return its result which would, in turn, be the actual argument passed to the function. However, this is not how \texttt{setq} works. The reason is that it is a special case that internally does this:

\begin{verbatim}
(set 'my-test-symbol "Protesilaos of Cyprus")
\end{verbatim}

This is where things are as expected. There is no magic happening behind the scenes. The \texttt{setq}, then, is a convenience for the user to not quote the symbol each time. Yes, this makes it a bit more difficult to reason about it, though you get used to it and eventually it all makes sense. Hopefully, you will get used to such special forms, as you find them with \texttt{setq} but also with \texttt{defun}, among many others. Here is a function you have already seen:

\begin{verbatim}
(defun my-greet-person-from-country (name country)
  "Say hello to the person with NAME who lives in COUNTRY."
  (message "Hello %s of %s" name country))
\end{verbatim}

If the normal rules of evaluation applied, then the list of parametes should be quoted. Otherwise, you would expect \texttt{(name country)} to be interpreted as a function call with \texttt{name} as the symbol of the function and \texttt{country} as its argument which would also be a variable. But this is not what is happening because \texttt{defun} will internally treat that list of parameters as if it was quoted.

Another common scenario is with \texttt{let} (\hyperref[sec:org8aa5d4d]{Control flow with \texttt{if-let*} and friends}). Its general form is as follows:

\begin{verbatim}
;; This is pseudo-code
(let LIST-OF-LISTS-AS-VARIABLE-BINDINGS
  BODY-OF-THE-FUNCTION)
\end{verbatim}

The \texttt{LIST-OF-LISTS-AS-VARIABLE-BINDINGS} is a list in which each element is a list of the form \texttt{(SYMBOL VALUE)}. Here is some actual code:

\begin{verbatim}
(let ((name "Protesilaos")
      (country "Cyprus"))
  (message "Hello %s of %s" name country))
\end{verbatim}

Continuing with the theme of special forms, if \texttt{let} was a typical function call, the \texttt{LIST-OF-LISTS-AS-VARIABLE-BINDINGS} would have to be quoted. Otherwise, it would be evaluated, in which case the first element would be the name of the function. But that would return an error, as the name of the function would correspond to another list, the \texttt{(name "Protesilaos")}, rather than a symbol. Things work fine with \texttt{let} because it internally does the quoting of its \texttt{LIST-OF-LISTS-AS-VARIABLE-BINDINGS}.

Expect similar behaviour with many special forms as well as with macros such as the popular \texttt{use-package}, which is used to configure packages inside of your Emacs initialisation file. How each of those macros works depends on the way it is designed. I will not delve into the technicalities here, as I want the book to be useful long-term, focusing on the principles rather than the implementation details that might change over time.

To learn what a given macro actually expands to, place the cursor at the end of its closing parenthesis and call the command \texttt{pp-macroexpand-last-sexp}. It will produce a new buffer showing the expanded Emacs Lisp code. This is what is actually evaluated in the macro's stead.

With those granted, it is time to write a macro. This is like a template, which empowers you to not repeat yourself. Syntactically, a macro will most probably depend on the use of the quasi-quote, the comma operator, and the mechanics of splicing (\hyperref[sec:org771b57a]{Partial evaluation inside of a list}). Here is a simple scenario where we want to run some code in a temporary buffer while setting the \texttt{default-directory} to the user's home directory.

\begin{verbatim}
(defmacro my-work-in-temp-buffer-from-home (&rest expressions)
  "Evaluate EXPRESSIONS in a temporary buffer with `default-directory' set to the user's home."
  `(let ((default-directory ,(expand-file-name "~/")))
     (with-temp-buffer
       (message "Running all expression from the `%s' directory" default-directory)
       ,@expressions)))
\end{verbatim}

In this definition, the \texttt{\&rest} makes the following parameter a list. So you can pass an arbitrary number of arguments to it, all of which will be collected into a single list called \texttt{EXPRESSIONS}. The judicious use of partial evaluation ensures that the macro will not be evaluated right now but only when it is called. The arguments passed to it will be placed where you have specified. Here is a call that uses this macro:

\begin{verbatim}
(progn
  (message "Now we are doing something unrelated to the macro")
  (my-work-in-temp-buffer-from-home
   (message "We do stuff inside the macro")
   (+ 1 1)
   (list "Protesilaos" "Cyprus")))
\end{verbatim}

If you place the cursor at the closing parenthesis of \texttt{my-work-in-temp-buffer-from-home}, you will be able to confirm what it expands to by typing M-x (\texttt{execute-extended-command}) and then invoking the command \texttt{pp-macroexpand-last-sexp}. This is what I get:

\begin{verbatim}
(let ((default-directory "/home/prot/"))
  (with-temp-buffer
    (message "Running all expression from the `%s' directory" default-directory)
    (message "We do stuff inside the macro")
    (+ 1 1)
    (list "Protesilaos" "Cyprus")))
\end{verbatim}

Piecing it together with the rest of the code in its context, I arrive at this:

\begin{verbatim}
(progn
  (message "Now we are doing something unrelated to the macro")
  (let ((default-directory "/home/prot/"))
    (with-temp-buffer
      (message "Running all expression from the `%s' directory" default-directory)
      (message "We do stuff inside the macro")
      (+ 1 1)
      (list "Protesilaos" "Cyprus"))))
\end{verbatim}

With this example in mind, consider Elisp macros to be a way of saying ``this little thing here helps me express this larger procedure more succinctly, while the actual code that runs is still that of the latter.''

The above macro I wrote has its body start with a quasi-quote, so you do not get to appreciate the nuances of evaluation within it. Let me show you this other approach, instead, where I write a macro that lets me define several almost identical interactive functions (\hyperref[sec:org10592f7]{Make your interactive function also work from Lisp calls}).

\begin{verbatim}
(defmacro my-define-command (name &rest expressions)
  "Define command with specifier NAME that evaluates EXPRESSIONS."
  (declare (indent 1))
  (unless (symbolp name)
    (error "I want NAME to be a symbol"))
  (let ((modifined-name (format "modified-version-of-%s" name)))
    `(defun ,(intern modifined-name) ()
       (interactive)
       ,(message "The difference between `%s' and `%s'" modifined-name name)
       ,@expressions)))
\end{verbatim}

The \texttt{my-define-command} can be broadly divided into two parts: (i) what gets evaluated outright and (ii) what gets expanded for further evaluation. The latter part starts with the quasi-quote. This distinction is important when we call the macro, because the former part will be executed right away so if we hit the error, it will never expand and then run the \texttt{EXPRESSIONS}. Try \texttt{pp-macroexpand-last-sexp} with the following to see what I mean. For your convenience, I include the macro expansions right below each case.

\begin{verbatim}
(my-define-command first-demo
  (message "This is what my function does")
  (+ 1 10)
  (message "And this"))
;; =>
;;
;; (defun modified-version-of-first-demo nil
;;   (interactive)
;;   "The difference between ‘modified-version-of-first-demo’ and ‘first-demo’"
;;   (message "This is what my function does")
;;   (+ 1 10)
;;   (message "And this"))


(my-define-command second-demo
  (list "Protesilaos" "Cyprus")
  (+ 1 1)
  (message "Arbitrary expressions here"))
;; =>
;;
;; (defun modified-version-of-second-demo nil
;;   (interactive)
;;   "The difference between ‘modified-version-of-second-demo’ and ‘second-demo’"
;;   (list "Protesilaos" "Cyprus")
;;   (+ 1 1)
;;   (message "Arbitrary expressions here"))


(my-define-command "error scenario"
  (list "Will" "Not" "Reach" "This")
  (/ 2 0))
;; => ERROR...
\end{verbatim}

Do you need macros? Not always, though there will be cases where a well-defined macro makes your code more elegant. What matters is that you have a sense of how evaluation works so that you do not get confused by all those parentheses. Otherwise you might expect something different to happen than what you actually get.
\section{Mapping through a list of elements}
\label{sec:org29370c2}
A common routine in programming is to work through a list of items and perform some computation on each of them. Emacs Lisp has the generic \texttt{while} loop, as well as a whole range of more specialised functions to map over a list of elements, such as \texttt{mapcar}, \texttt{mapc}, \texttt{dolist}, \texttt{seq-filter}, \texttt{seq-remove}, and many more. Depending on what you are doing, you map through elements with the intent to produce some side effect and/or to test for a return value (\hyperref[sec:org2689823]{Side effect and return value}). I will show you some examples and let you decide which is the most appropriate tool for the task at hand.

Starting with \texttt{mapcar}, it applies a function to each element of a list. It then takes the return value at each iteration and collects it into a new list. This is the return value of \texttt{mapcar} as a whole. In the following code block, I use \texttt{mapcar} over a list of numbers to increment them by \texttt{10} and return a new list of the incremented numbers.

\begin{verbatim}
(mapcar
 (lambda (number)
   (+ 10 number))
 '(1 2 3 4 5))
;; => (11 12 13 14 15)
\end{verbatim}

In the code block above, I am using a \texttt{lambda}, else an anonymous function (\hyperref[sec:org1669468]{When to use a named function or a lambda function}). Here is the same code, but with an eponymous function, i.e. a named function:

\begin{verbatim}
(defun my-increment-by-ten (number)
  "Add 10 to NUMBER."
  (+ 10 number))

(mapcar #'my-increment-by-ten '(1 2 3 4 5))
;; => (11 12 13 14 15)
\end{verbatim}

Notice that here we quote the eponymous function (\hyperref[sec:org9b7817a]{Symbols, balanced expressions, and quoting}).

The \texttt{mapcar} collects the return values into a new list. Sometimes this is useless. Suppose you want to evaluate a function that saves all unsaved buffers which visit a file. In this scenario, you do not care about accumulating the results: you just want the side effect of saving the buffer outright. To this end, you may use \texttt{mapc}, which always returns the list it operated on:

\begin{verbatim}
(mapc
 (lambda (buffer)
   (when (and (buffer-file-name buffer)
              (buffer-modified-p buffer))
     (save-buffer)))
 (buffer-list))
\end{verbatim}

An alternative to the above is \texttt{dolist}, which is used for side effects but always returns \texttt{nil}:

\begin{verbatim}
(dolist (buffer (buffer-list))
  (when (and (buffer-file-name buffer)
             (buffer-modified-p buffer))
    (save-buffer)))
\end{verbatim}

You will notice that the \texttt{dolist} is a macro, so some parts of it seem to behave differently than with basic lists and the evaluation rules that apply to them (\hyperref[sec:org61e1a1b]{Evaluation inside of a macro or special form}). This is a matter of getting used to how the code is expressed.

When to use a \texttt{dolist} as opposed to a \texttt{mapc} is a matter of style. If you are using a named function, a \texttt{mapc} looks cleaner to my eyes. Otherwise a \texttt{dolist} is easier to read. Here is my approach with some pseudo-code:

\begin{verbatim}
;; I like this:
(mapc #'NAMED-FUNCTION LIST)

;; I also like a `dolist' instead of a `mapc' with a `lambda':
(dolist (element LIST)
  (OPERATE-ON element))

;; I do not like this:
(mapc
 (lambda (element)
   (OPERATE-ON element))
 LIST)
\end{verbatim}

While \texttt{dolist} and \texttt{mapc} are for side effects, you can still employ them in the service of accumulating results, with the help of \texttt{let} and related forms (\hyperref[sec:org8aa5d4d]{Control flow with \texttt{if-let*} and friends}). Depending on the specifics, this approach may make more sense than relying on a \texttt{mapcar}. Here is an annotated sketch:

\begin{verbatim}
;; Start with an empty list of `found-strings'.
(let ((found-strings nil))
  ;; Use `dolist' to test each element of the list '("Protesilaos" 1 2 3 "Cyprus").
  (dolist (element '("Protesilaos" 1 2 3 "Cyprus"))
    ;; If the element is a string, then `push' it to the `found-strings', else skip it.
    (when (stringp element)
      (push element found-strings)))
  ;; Now that we are done with the `dolist', return the new value of `found-strings'.
  found-strings)
;; => ("Cyprus" "Protesilaos")


;; As above but reverse the return value, which makes more sense:
(let ((found-strings nil))
  (dolist (element '("Protesilaos" 1 2 3 "Cyprus"))
    (when (stringp element)
      (push element found-strings)))
  (nreverse found-strings))
;; => ("Protesilaos" "Cyprus")
\end{verbatim}

For completeness, the previous example would have to be done as follows with the use of \texttt{mapcar}:

\begin{verbatim}
(mapcar
 (lambda (element)
   (when (stringp element)
     element))
 '("Protesilaos" 1 2 3 "Cyprus"))
;; => ("Protesilaos" nil nil nil "Cyprus")


(delq nil
      (mapcar
       (lambda (element)
         (when (stringp element)
           element))
       '("Protesilaos" 1 2 3 "Cyprus")))
;; => ("Protesilaos" "Cyprus")
\end{verbatim}

Because \texttt{mapcar} happily accumulates all the return values, it returns a list that includes \texttt{nil}. If you wanted that, you would probably not even bother with the \texttt{when} clause there. The \texttt{delq} is thus applied to the return value of the \texttt{mapcar} to delete all the instances of \texttt{nil}. Now compare this busy work to \texttt{seq-filter}:

\begin{verbatim}
(seq-filter #'stringp '("Protesilaos" 1 2 3 "Cyprus"))
;; => ("Protesilaos" "Cyprus")
\end{verbatim}

The \texttt{seq-filter} is the best tool when all you need is to test if the element satisfies a predicate function and then return that element. But you cannot return something else. Whereas \texttt{mapcar} will take any return value without complaints, such as the following:

\begin{verbatim}
(delq nil
      (mapcar
       (lambda (element)
         (when (stringp element)
           ;; `mapcar' accumulates any return value, so we can change
           ;; the element to generate the results we need.
           (upcase element)))
       '("Protesilaos" 1 2 3 "Cyprus")))
;; => ("PROTESILAOS" "CYPRUS")

(seq-filter
 (lambda (element)
   (when (stringp element)
     ;; `seq-filter' only returns elements that have a non-nil return
     ;; value here, but it returns the elements, not what we return
     ;; here.  In other words, this `lambda' does unnecessary work.
     (upcase element)))
 '("Protesilaos" 1 2 3 "Cyprus"))
;; => ("Protesilaos" "Cyprus")
\end{verbatim}

How you go about mapping over a list of elements will depend on what you are trying to do. There is no one single function that does everything for you. Understand the nuances and you are good to go. Oh, and do look into the built-in \texttt{seq} library (use M-x (\texttt{execute-extended-command}), invoke \texttt{find-library}, and then search for \texttt{seq}). You are now looking at the source code of \texttt{seq.el}: it defines plenty of functions like \texttt{seq-take}, \texttt{seq-find}, \texttt{seq-union}. Another way is to invoke the command \texttt{shortdoc} and read about the documentation groups \texttt{list} as well as \texttt{sequence}.
\section{The match data of the last search}
\label{sec:orge641c75}
As you work with Emacs Lisp, you will encounter the concept of ``match data'' and the concomitant functions \texttt{match-data}, \texttt{match-beginning}, \texttt{match-string}, and so on. These refer to the results of the last search, which is typically performed by the functions \texttt{re-search-forward}, \texttt{looking-at}, \texttt{string-match}, and related. Each time you perform a search, the match data gets updated. Be mindful of this common side effect (\hyperref[sec:org2689823]{Side effect and return value}). If you forget about it, chances are your code will not do the right thing.

In the following code block, I define a function that performs a search in the current buffer and returns a list of match data without text properties, where relevant (\hyperref[sec:orgcf4e6e5]{Text has its own properties}).

\begin{verbatim}
(defun my-get-match-data (regexp)
  "Search forward for REGEXP and return its match data, else nil."
  (when (re-search-forward regexp nil t)
    (list
     :beginning (match-beginning 0)
     :end (match-end 0)
     :string (match-string-no-properties 0))))
\end{verbatim}

You may then call it with a string argument, representing an Emacs Lisp regular expression:

\begin{verbatim}
(my-get-match-data "Protesilaos.*Cyprus")
\end{verbatim}

If the regular expression matches, then you get the match data. Here is some sample text:

\begin{verbatim}
Protesilaos lives in the mountains of Cyprus.
\end{verbatim}

Place the cursor before that text and use M-: (\texttt{eval-expression}) to evaluate \texttt{my-get-match-data} with the regexp I show above. You will get a return value, as intended.

The way \texttt{my-get-match-data} is written, it does two things: (i) it has the side effect of moving the cursor to the end of the text it found and (ii) it returns a list with the match data I specified. There are many scenaria where you do not want the aforementioned side effect: the cursor should stay where it is. As such, you can wrap your code in a \texttt{save-excursion} (\hyperref[sec:org518036f]{Switching to another buffer, window, or narrowed state}): it will do what it must and finally restore the \texttt{point} (\hyperref[sec:orgd0c57f0]{Run some code or fall back to some other code}):

\begin{verbatim}
(defun my-get-match-data (regexp)
  "Search forward for REGEXP and return its match data, else nil."
  (save-excursion ; we wrap our code in a `save-excursion' to inhibit the side effect
    (when (re-search-forward regexp nil t)
      (list
       :beginning (match-beginning 0)
       :end (match-end 0)
       :string (match-string-no-properties 0)))))
\end{verbatim}

If you evaluate this version of \texttt{my-get-match-data} and then retry the function call I had above, you will notice how you get the expected return value without the side effect of the cursor moving to the end of the matching text. In practice, this is a useful tool that may be combined with \texttt{save-match-data}. Imagine you want to do a search forward inside of another search you are performing, such as to merely test if there is a match for a regular expression in the context, but need to inhibit the modification of the match data you planned to operate on. As such:

\begin{verbatim}
(defun my-get-match-data-with-extra-check (regexp)
  "Search forward for REGEXP followed by no spaces and return its match data, else nil."
  (save-excursion
    (when (and (re-search-forward regexp nil t)
               (save-match-data (not (looking-at "[\s\t]+"))))
      ;; Return the match data of the first search.  The second one
      ;; which tests for spaces or tabs is just an extra check, but we
      ;; do not want to use its match data, hence the `save-match-data'
      ;; around it.
      (list
       :beginning (match-beginning 0)
       :end (match-end 0)
       :string (match-string-no-properties 0)))))
\end{verbatim}

Evaluate the function \texttt{my-get-match-data-with-extra-check} and then call with M-: (\texttt{eval-expression}) to test that it returns a non-\texttt{nil} value with the second example below, but not the first one. This is the expected outcome.

\begin{verbatim}
(my-get-match-data-with-extra-check "Protesilaos.*Cyprus")
;; => nil


;; Protesilaos, also known as "Prot", lives in the mountains of Cyprus   .

(my-get-match-data-with-extra-check "Protesilaos.*Cyprus")
;; => (:beginning 41988 :end 42032 :string "Protesilaos lives in the mountains of Cyprus")


;; Protesilaos lives in the mountains of Cyprus.
\end{verbatim}
\section{Switching to another buffer, window, or narrowed state}
\label{sec:org518036f}
As you use Emacs Lisp to do things programmatically, you encounter cases where you need to move away from where you are. You may have to switch to another buffer, change to the window of a given buffer, or even modify what is visible in the buffer you are editing. At all times, this involves one or more side effects which, most probably, should be undone when your function finishes its job (\hyperref[sec:org2689823]{Side effect and return value}).

Perhaps the most common case is to restore the \texttt{point}. You have some code that moves back or forth in the buffer to perform a match for a given piece of text. But then, you need to leave the cursor where it originally was, otherwise the user will lose their orientation. Wrap your code in a \texttt{save-excursion} and you are good to go, as I show elsewhere (\hyperref[sec:orge641c75]{The match data of the last search}):

\begin{verbatim}
(save-excursion ; restore the `point' after you are done
  MOVE-AROUND-IN-THIS-BUFFER)
\end{verbatim}

Same principle for \texttt{save-window-excursion}, which allows you to select another window, such as with \texttt{select-window}, move around in its buffer, and then restore the windows as they were:

\begin{verbatim}
(save-window-excursion
  (select-window SOME-WINDOW)
  MOVE-AROUND-IN-THIS-BUFFER)
\end{verbatim}

The \texttt{save-restriction} allows you to restore the current narrowing state of the buffer. You may then choose to either \texttt{widen} or \texttt{narrow-to-region} (and related commands like \texttt{org-narrow-to-subtree}), do what you must, and then restore the buffer to its original state.

\begin{verbatim}
;; Here we assume that we start in a widened state.  Then we narrow to
;; the current Org heading to get all of its contents as one massive
;; string.  Then we widen again, courtesy of `save-restriction'.
(save-restriction
  (org-narrow-to-subtree)
  (buffer-string))
\end{verbatim}

Depending on the specifics, you will want to combine the aforementioned. Beware that the documentation of \texttt{save-restriction} tells you to use \texttt{save-excursion} as the outermost call. Other than that, you will also find cases that require a different approach to perform some conditional behaviour (\hyperref[sec:orgd0c57f0]{Run some code or fall back to some other code}).
\section{Basic control flow with \texttt{if}, \texttt{cond}, and others}
\label{sec:org9074f8f}
You do not need any conditional logic to perform basic operations. For example, if you write a command that moves 15 lines down, it will naturally stop at the end of the buffer when it cannot move past the number you specified. Using \texttt{defun}, you write an interactive function (i.e. a ``command'') to unconditionally move down 15 lines using \texttt{forward-line} internally (call it with a negative number to move in the opposite direction):

\begin{verbatim}
(defun my-15-lines-down ()
  "Move at most 15 lines down."
  (interactive)
  (forward-line 15))
\end{verbatim}

The \texttt{my-15-lines-down} is about as simple as it gets: it wraps around a basic function and passes to it a fixed argument, in this case the number \texttt{15}. Use M-x (\texttt{execute-extended-command}) and then call this command by its name. It works! Things get more involved as soon as you decide to perform certain actions only once a given condition is met. This ``control flow'' between different branches of a logical sequence is expressed with \texttt{if}, \texttt{when}, \texttt{unless}, and \texttt{cond}, among others. Depending on the specifics of the case, \texttt{and} as well as \texttt{or} may suffice.

How about you make your \texttt{my-15-lines-down} a bit smarter? When it is at the absolute end of the buffer, have it move 15 lines up. Why? Because this is a demonstration, so why not? The predicate function that tests if the point is at the end of the buffer is \texttt{eobp}. A ``predicate'' is a function that returns true, technically non-\texttt{nil}, when its condition is met, else it returns \texttt{nil} (\hyperref[sec:org2689823]{Side effect and return value}). As for the weird name, the convention in Emacs Lisp is to end predicate functions with the \texttt{p} suffix: if the name of the function consists of multiple words, typically separated by dashes, then the predicate function is named \texttt{NAME-p}, like \texttt{string-match-p}, otherwise it is \texttt{NAMEp}, like \texttt{stringp}.

\begin{verbatim}
(defun my-15-lines-down-or-up ()
  "Move at most 15 lines down or go back if `eobp' is non-nil."
  (interactive)
  (if (eobp)
      (forward-line -15)
    (forward-line 15)))
\end{verbatim}

Evaluate this function, then type M-x (\texttt{execute-extended-command}) and invoke \texttt{my-15-lines-down-or-up} to get a feel for it. Below is a similar idea, which throws and error and exits what it was doing if \texttt{eobp} returns non-\texttt{nil}:

\begin{verbatim}
(defun my-15-lines-down-or-error ()
  "Throw an error if `eobp' returns non-nil, else move 15 lines down."
  (interactive)
  (if (eobp)
      (error "Already at the end; will not move further")
    (forward-line 15)))
\end{verbatim}

A quirk of Emacs Lisp, which may be a feature all along, is how indentation is done. Just mark the code you have written and type TAB: Emacs will take care to indent it the way it should be done. In the case of the \texttt{if} statement, the ``then'' part is further in than the ``else'' part of the logic. There is no special meaning to this indentation: you could write everything on a single line like \texttt{(if COND THIS ELSE)}, which looks like your typical list, by the way (\hyperref[sec:org9b7817a]{Symbols, balanced expressions, and quoting}). What the indentation does is help you identify imbalances in your parentheses. If the different expressions all line up in a way that looks odd, then you are most probably missing a parentheses or have too many of them. Generally, expressions at the same level will all line up the same way. Those deeper in will have more indentation, and so on. Experience will allow you to spot mistakes with mismatching parentheses. But even if you do not identify them, you will get an error eventually. Rest assured!

The way \texttt{if} is written is like a function that takes two or more arguments. The ``or more'' all counts as part of the ``else'' logic. As such, \texttt{(if COND THIS)} has no ``else'' consequence, while \texttt{(if COND THIS ELSE1 ELSE2 ELSE3)} will run \texttt{ELSE1}, \texttt{ELSE2}, and \texttt{ELSE3} in order as part of the ``else'' branch. Here is how this looks once you factor in proper indentation:

\begin{verbatim}
(if COND
    THIS
  ELSE1
  ELSE2
  ELSE3)
\end{verbatim}

Now what if the \texttt{THIS} part needs to be more than one function call? Elisp has the \texttt{progn} form, which you can use to wrap function calls and pass them as a single argument. Putting it all together, your code will now look this like:

\begin{verbatim}
(if COND
    (progn
      THIS1
      THIS2
      THIS3)
  ELSE1
  ELSE2
  ELSE3)
\end{verbatim}

If you do not need the ``else'' part, use \texttt{when} to express your intention. Internally, this is a macro which actually stands for \texttt{(if COND (progn EXPRESSIONS))}, where \texttt{EXPRESSIONS} is one or more expressions. A \texttt{when} looks like this:

\begin{verbatim}
(when COND
  THIS1
  THIS2
  THIS3)
\end{verbatim}

Similarly, the \texttt{unless} has the meaning of \texttt{(when (not COND) EXPRESSIONS)}. It, too, is a macro that expands to an \texttt{if} statement:

\begin{verbatim}
(unless COND
  THIS1
  THIS2
  THIS3)
\end{verbatim}

When the condition you are testing for has multiple parts, you can rely on \texttt{and} as well as \texttt{or}:

\begin{verbatim}
(when (or THIS THAT)
  EXPRESSIONS)

(when (and THIS THAT)
  EXPRESSIONS)

(when (or (and THIS THAT) OTHER)
  EXPRESSIONS)
\end{verbatim}

Depending on the specifics of the case, the combination of multiple \texttt{if}, \texttt{when}, \texttt{or}, \texttt{and} will look awkward. You can break down the logic to distinct conditions, which are tested in order from top to bottom, using \texttt{cond}. The way \texttt{cond} is written is as a list of lists, which do not need quoting (\hyperref[sec:org61e1a1b]{Evaluation inside of a macro or special form}). In abstract, it looks like this:

\begin{verbatim}
(cond
 (CONDITION1
  CONSEQUENCES1)
 (CONDITION2
  CONSEQUENCES2)
 (CONDITION3
  CONSEQUENCES3)
 (t
  CONSEQUENCES-FALLBACK))
\end{verbatim}

Each of the consequences can be any number of expressions, like you saw above with \texttt{when}. This is a toy function to show how \texttt{cond} behaves:

\begin{verbatim}
(defun my-toy-cond (argument)
  "Return a response depending on the type of ARGUMENT."
  (cond
   ((and (stringp argument)
         (string-blank-p argument))
    (message "You just gave me a blank string; try harder!"))
   ((stringp argument)
    (message "I see you can do non-blanks string; I call that progress."))
   ((null argument)
    (message "Yes, the nil is an empty list like (), but do not worry about it"))
   ((listp argument)
    (message "Oh, I see you are in the flow of using lists!"))
   ((symbolp argument)
    (message "What's up with the symbols, mate?"))
   ((natnump argument)
    (message "I fancy those natural numbers!"))
   ((numberp argument)
    (message "You might as well be a math prodigy!"))
   (t
    (message "I have no idea what type of thing your argument `%s' is" argument))))
\end{verbatim}

I want you to evaluate it and pass it different arguments to test what it does (\hyperref[sec:org91f7fd8]{Evaluate Emacs Lisp}). Here are two examples:

\begin{verbatim}
(my-toy-cond "")
;; => "You just gave me a blank string; try harder!"

(my-toy-cond '(1 2 3))
;; => "Oh, I see you are in the flow of using lists!"
\end{verbatim}

All of the above are common in Emacs Lisp. Another powerful macro is \texttt{pcase}, which we will consider separately due to its particularities (\hyperref[sec:orgc256332]{Pattern match with \texttt{pcase} and related}).
\section{Control flow with \texttt{if-let*} and friends}
\label{sec:org8aa5d4d}
The \texttt{let} and \texttt{let*} declare variables that are available only within the current scope, else the \texttt{BODY} of the \texttt{let}. As such:

\begin{verbatim}
(let BINDINGS
  BODY)

(let ((variable1 value1)
      (variable2 value2))
  BODY)
\end{verbatim}

The \texttt{BINDINGS} is a list of lists, which does not need to be quoted (\hyperref[sec:org61e1a1b]{Evaluation inside of a macro or special form}). While \texttt{BODY} consists of one or more expressions, which I have also named \texttt{EXPRESSIONS} elsewhere in this book. The difference between \texttt{let} and \texttt{let*} (pronounced ``let star'') is that the latter makes earlier bindings available to later bindings. Like this:

\begin{verbatim}
;; This works because `greeting' can access `name' and `country',
;; courtesy of `let*':
(let* ((name "Protesilaos")
       (country "Cyprus")
       (greeting (format "Hello %s of %s" name country)))
  (DO-STUFF-WITH greeting))

;; But this fails...
(let ((name "Protesilaos")
      (country "Cyprus")
      (greeting (format "Hello %s of %s" name country)))
  (DO-STUFF-WITH greeting))
\end{verbatim}

Sometimes what you want to do is create those bindings if---and only if---they are all non-\texttt{nil}. If their value is \texttt{nil}, then they are useless to you, in which case you do something else (\hyperref[sec:org9074f8f]{Basic control flow with \texttt{if}, \texttt{cond}, and others}). Values may or may not be \texttt{nil} when you are creating a binding with the return value of a function call or some other variable. You could always write code like this:

\begin{verbatim}
(let ((variable1 (SOME-FUNCTION SOME-ARGUMENT))
      (variable2 (OTHER-FUNCTION OTHER-ARGUMENT)))
  (if (and variable1 variable2) ; simply test both for non-nil
      THIS
    ELSE))
\end{verbatim}

But you can do the same with \texttt{if-let*}, where the \texttt{THIS} part runs only if all the bindings are non-\texttt{nil}:

\begin{verbatim}
(if-let* ((variable1 (SOME-FUNCTION SOME-ARGUMENT))
          (variable2 (OTHER-FUNCTION OTHER-ARGUMENT)))
    THIS
  ELSE)
\end{verbatim}

In the \texttt{ELSE} part, the bindings \texttt{variable1} and \texttt{variable2} do not exist: they only exist for the \texttt{THIS} part of the code.

The \texttt{when-let*} is the same as \texttt{when}, meaning that it has no ``else'' logic. If one of its bindings is \texttt{nil}, then the whole \texttt{when-let*} returns \texttt{nil}. No need to belabour that point.

As you dig dipper into the Emacs Lisp ecosystem, you will come across uses of \texttt{if-let*} that (i) create multiple bindings like \texttt{let} or \texttt{let*} but (ii) also call a predicate function to test if they should continue with the \texttt{THIS} part of their logic. Remember that \texttt{if-let*} goes straight to \texttt{ELSE} if one of its bindings returns \texttt{nil}. Consider this example:

\begin{verbatim}
(if-let* ((variable1 (SOME-FUNCTION SOME-ARGUMENT))
          ;; The _ signifies intent: "do not bind this; I only care
          ;; about the return value being non-nil".  What we are doing
          ;; here is test if `variable1' is a string: if it is, we
          ;; continue with the bindings, otherwise we move to the ELSE
          ;; part of the code.
          (_ (string-match-p variable1))
          (variable2 (OTHER-FUNCTION OTHER-ARGUMENT)))
    THIS
  ELSE)
\end{verbatim}

There is no inherently superior way of doing things. It is a matter of using the right tool for the task at hand. Sometimes you want the bindings to be created, even if their value is \texttt{nil}. Choose what makes sense.
\section{Pattern match with \texttt{pcase} and related}
\label{sec:orgc256332}
Once you get in the flow of expressing your thoughts with Emacs Lisp, you will be fluent in the use of \texttt{if}, \texttt{cond}, and the like (\hyperref[sec:org9074f8f]{Basic control flow with \texttt{if}, \texttt{cond}, and others}). You might even get more fancy if \texttt{if-let*} (\hyperref[sec:org8aa5d4d]{Control flow with \texttt{if-let*} and friends}). However you go about it, there are some cases that arguably benefit from more succinct expressions. This is where \texttt{pcase} comes in. At its more basic formulation, it is like \texttt{cond}, in that it tests the return value of a given expression against a list of conditions. Here is an example that compared the buffer-local value of the variable \texttt{major-mode} for equality against a couple of known symbols:

\begin{verbatim}
(pcase major-mode
  ('org-mode (message "You are in Org"))
  ('emacs-lisp-mode (message "You are in Emacs Lisp"))
  (_ (message "You are somewhere else")))
\end{verbatim}

The above is the same idea as this \texttt{cond}:

\begin{verbatim}
(cond
 ((eq major-mode 'org-mode)
  (message "You are in Org"))
 ((eq major-mode 'emacs-lisp-mode)
  (message "You are in Emacs Lisp"))
 (t
  (message "You are somewhere else")))
\end{verbatim}

Some programmers may argue that \texttt{pcase} is more elegant. I think it is true in this specific example, though I remain flexible and practical: I will use whatever makes more sense for the code I am writing. While on the topic of elegance, I should inform you that practically all of the conditional logic can be done in a way that may seem unexpected. Consider how my examples in this book make repetitive use of \texttt{message}, when in reality the only part that changes is the actual string/argument passed to that function. This will work just as well:

\begin{verbatim}
(message
 (pcase major-mode
   ('org-mode "You are in Org")
   ('emacs-lisp-mode "You are in Emacs Lisp")
   (_ "You are somewhere else")))
\end{verbatim}

Same idea for \texttt{if}, \texttt{when}, and the rest.

Back to the topic of what \texttt{pcase} does differently. If you read its documentation, you will realise that it has its own mini language, or ``domain-specific language'' (DSL). This is common for macros (\hyperref[sec:org61e1a1b]{Evaluation inside of a macro or special form}). They define how evaluation is done and what sort of expressions are treated specially. Let me then gift you this toy function that illustrates some of the main features of the DSL now under consideration:

\begin{verbatim}
(defun my-toy-pcase (argument)
  "Use `pcase' to return an appropriate response for ARGUMENT."
  (pcase argument
    (`(,one ,_ ,three)
     (message "List where first element is `%s', second is ignored, third is `%s'" one three))
    (`(,one . ,two)
     (message "Cons cell where first element is `%s' and second is `%s'" one two))
    ((pred stringp)
     (message "The argument is a string of some sort"))
    ('hello
     (message "The argument is equal to the symbol `hello'"))
    (_ (message "This is the fallback"))))
\end{verbatim}

Go ahead and evaluate that function and then try it out (\hyperref[sec:org91f7fd8]{Evaluate Emacs Lisp}). Below are a couple of examples:

\begin{verbatim}
(my-toy-pcase '("Protesilaos" "of" "Cyprus"))
;; => "List where first element is ‘Protesilaos’, second is ignored, third is ‘Cyprus’"

(my-toy-pcase '("Protesilaos" . "Cyprus"))
;; => "Cons cell where first element is ‘Protesilaos’ and second is ‘Cyprus’"
\end{verbatim}

Some of those clauses are a different way to express \texttt{cond}. Arguably better, but not a clear winner in my opinion. What is impressive and a true paradigm shift is the concept of ``destructuring'', else the pattern matching done to the expression that effectively \texttt{let} binds elements of a list or cons cell to their corresponding index. The syntax used for this destructuring is arcane, until you relate it to the quasi-quote and the comma which are used for partial evaluation (\hyperref[sec:org771b57a]{Partial evaluation inside of a list}). With this in mind, consider \texttt{pcase-let}, \texttt{pcase-let*}, \texttt{pcase-lambda}, and \texttt{pcase-dolist}, as variations of the plain \texttt{let}, \texttt{let*}, \texttt{lambda}, and \texttt{dolist} with the added feature of supporting destructuring. They are not doing any of the extras of \texttt{pcase} though---just destructuring on top of their familiar behaviour! This is especially useful when you are working with the return value of a function which comes as a list. I will not elaborate at length, as this is an advanced use-case. If you are already at that level, you do not need me to tell you what to write. For the rest of us who, like me, typically work with simpler code, the \texttt{pcase-let} serves as a sufficient illustration of the principle:

\begin{verbatim}
(defun my-split-string-at-space (string)
  "Split STRING at the space, returning a list of strings."
  (split-string string "\s"))

(pcase-let ((`(,one ,_ ,three) (my-split-string-at-space "Protesilaos of Cyprus")))
  (message "This is like `let', but we got `%s' and `%s' via destructuring" one three))
;; => "This is like ‘let’, but we got ‘Protesilaos’ and ‘Cyprus’ via destructuring"
\end{verbatim}

Whether you use \texttt{pcase} and destructuring in general is up to you. You do not require them to write high quality code. Though you might agree with those who consider them inherently more elegant and opt to use them for this very reason to have code that is succinct yet highly expressive.
\section{Run some code or fall back to some other code}
\label{sec:orgd0c57f0}
Your typical code will rely on \texttt{if}, \texttt{cond}, and the like for control flow (\hyperref[sec:org9074f8f]{Basic control flow with \texttt{if}, \texttt{cond}, and others}). Depending on your specific needs or stylistic considerations, it may even include \texttt{pcase} (\hyperref[sec:orgc256332]{Pattern match with \texttt{pcase} and related}) as well as \texttt{if-let*} (\hyperref[sec:org8aa5d4d]{Control flow with \texttt{if-let*} and friends}). There are some cases, nonetheless, that make it imperative you run additional code after your primary operation concludes or exits. The idea is to clean up whatever intermediate state you created. The logic is ``do this with all the necessary side effects, then whatever happens to it do that now to, inter alia, undo the side effects.'' This is the concept of ``unwinding'', which is implemented via \texttt{unwind-protect}.

In the following code block, I define a function which produces a minibuffer prompt asking you to provide a \texttt{y} or \texttt{n} answer, which is shorthand notation for ``yes'' or ``no''. It tests the return value of \texttt{y-or-n-p} to determine what it needs to do. While the prompt is open, the function highlights all instances of the regular expression \texttt{(defun} in the current buffer. Those highlights must go away after you are done with the minibuffer and its consequences.

\begin{verbatim}
(defun my-prompt-with-temporary-highlight ()
  "Ask for confirmation and highlight all instances of a regexp while waiting."
  (let ((regexp "(defun"))
    (unwind-protect
        (progn
          (highlight-regexp regexp)
          (if (y-or-n-p "Should we proceed or not? ")
              (message "You have decided to proceed")
            (message "You prefer not to continue")))
      (unhighlight-regexp regexp))))
\end{verbatim}

Try the above in your Emacs to get a feel for it. While the ``yes or no'' prompt is active, also do C-g (\texttt{keyboard-quit}) or C-] (\texttt{abort-recursive-edit}) to confirm that the highlights are removed even though the code never gets past the prompting phase. You may even modify the function to produce an error: it will create a backtrace, which will still have the effect of unwinding after you do q (\texttt{debugger-quit}) from the \texttt{*Backtrace*} window.

\begin{verbatim}
(defun my-prompt-with-temporary-highlight-try-with-error ()
  "Ask for confirmation and highlight all instances of a regexp while waiting."
  (let ((regexp "(defun"))
    (unwind-protect
        (progn
          (highlight-regexp regexp)
          (error "This error makes no sense here; close the backtrace to test the unwinding")
          (if (y-or-n-p "Should we proceed or not? ")
              (message "You have decided to proceed")
            (message "You prefer not to continue")))
      (unhighlight-regexp regexp))))
\end{verbatim}

Taking a step back, you will figure out how \texttt{unwind-protect} is a more general form of specialists like \texttt{save-excursion} and \texttt{save-restriction} (\hyperref[sec:org518036f]{Switching to another buffer, window, or narrowed state}), while it underpins the \texttt{save-match-data} (\hyperref[sec:orge641c75]{The match data of the last search}) among many other functions/macros, such as \texttt{with-temp-buffer} and \texttt{save-window-excursion}. What \texttt{unwind-protect} does not do is respond specially to signals, such as those coming from the \texttt{error} function: it will allow the error to happen, meaning that a backtrace will be displayed and your code will exit right there (but the unwinding will still work, as I already explained, once you dismiss the backtrace). To make your code treat signals in a more controlled fashion, you must rely on \texttt{condition-case}.

With \texttt{condition-case} you assume full control over the behaviour of your code, including how it should deal with errors. Put differently, your Elisp will express the intent of ``I want to do this, but if I get an error I want to do that instead.'' There are many signals to consider, all of which come from the \texttt{signal} function. These include the symbols \texttt{error}, \texttt{user-error}, \texttt{args-out-of-range}, \texttt{wrong-type-argument}, \texttt{wrong-length-argument}, and \texttt{quit}, in addition to anything else the programmer may consider necessary. In the following code blocks, I show you how \texttt{condition-case} looks like. Remember that sometimes you do not do quoting the usual way because of how the underlying form is implemented (\hyperref[sec:org61e1a1b]{Evaluation inside of a macro or special form}). The example I am using is the same I had for \texttt{unwind-protect}.

\begin{verbatim}
(defun my-prompt-with-temporary-highlight-and-signal-checks ()
  "Ask for confirmation and highlight all instances of a regexp while waiting."
  (let ((regexp "(defun"))
    (condition-case nil
        (progn
          (highlight-regexp regexp)
          (if (y-or-n-p "Should we proceed or not? ")
              (user-error "You have decided to proceed; but we need to return a `user-error'")
            (error "You prefer not to continue; but we need to return an `error'")))
      (:success
       (unhighlight-regexp regexp)
       (message "No errors, but still need to unwind what we did, plus whatever else we want here"))
      (quit
       (unhighlight-regexp regexp)
       (message "This is our response to the user aborting the prompt"))
      (user-error
       (unhighlight-regexp regexp)
       (message "This is our response to the `user-error' signal"))
      (error
       (unhighlight-regexp regexp)
       (message "This is our response to the `error' signal")))))
\end{verbatim}

The above function illustrates both the aforementioned concept of unwinding and the mechanics of handling signals. The abstract structure of \texttt{condition-case} looks to me like an amalgamation of \texttt{let}, \texttt{unwind-protect}, and \texttt{cond}. These conditions may include the special handler of \texttt{:success}, as I show there. Granted, the code I wrote will never lead to that specific success case, though you can modify what happens after the prompt to, say, call \texttt{message} instead of the \texttt{user-error} function, which will then count as a successful conclusion. Otherwise, I think the expressions I wrote tell you exactly how this program responds to the signals it receives.

What I have not covered yet, is the aspect of \texttt{condition-case} that is like the \texttt{let}, namely, how it binds the error data to a variable within this scope. In my implementation above, it is the \texttt{nil} you see there, meaning that I choose not to perform such a binding, as I have no use for its data. Below I decide to use it, just for the sake of demonstration.

\begin{verbatim}
(defun my-prompt-with-temporary-highlight-and-signal-checks-with-error-report ()
  "Ask for confirmation and highlight all instances of a regexp while waiting."
  (let ((regexp "(defun"))
    (condition-case error-data-i-got
        (progn
          (highlight-regexp regexp)
          (if (y-or-n-p "Should we proceed or not? ")
              (user-error "You have decided to proceed; but we need to return a `user-error'")
            (error "You prefer not to continue; but we need to return an `error'")))
      (:success
       (unhighlight-regexp regexp)
       (message "No errors, but still need to unwind what we did, plus whatever else we want here")
       (message "The error is `%s' and its data is `%S'" (car error-data-i-got) (cdr error-data-i-got)))
      (quit
       (unhighlight-regexp regexp)
       (message "This is our response to the user aborting the prompt")
       (message "The error is `%s' and its data is `%S'" (car error-data-i-got) (cdr error-data-i-got)))
      (user-error
       (unhighlight-regexp regexp)
       (message "This is our response to the `user-error' signal")
       (message "The error is `%s' and its data is `%S'" (car error-data-i-got) (cdr error-data-i-got)))
      (error
       (unhighlight-regexp regexp)
       (message "This is our response to the `error' signal")
       (message "The error is `%s' and its data is `%S'" (car error-data-i-got) (cdr error-data-i-got))))))
\end{verbatim}

There will be times when \texttt{unwind-protect} and \texttt{condition-case} are the right tools for the job. My hope is that these examples have given you the big picture view and you are now ready to write your own programs in Emacs Lisp.
\section{When to use a named function or a lambda function}
\label{sec:org1669468}
The \texttt{lambda} is an anonymous function. It stands in juxtaposition to \texttt{defun}, which defines a function with a given name. When to use one or the other is largely a matter of style. Though there are some cases where a certain approach is more appropriate. The rule of thumb is this: if you need to use the function more than once, then give it a name and then call it by its name. Otherwise, you will effectively be redefining it each time, which makes it hard for you to rewrite your program. By contrast, if the function is only relevant ad-hoc, then a \texttt{lambda} is fine.

In some cases, you will have a named function that employs a \texttt{lambda} internally. To modify one of the examples you will find in this book (\hyperref[sec:org29370c2]{Mapping through a list of elements}):

\begin{verbatim}
(defun my-increment-numbers-by-ten (numbers)
  "Add 10 to each number in NUMBERS and return the new list."
  (mapcar
   (lambda (number)
     (+ 10 number))
   numbers))

(my-increment-numbers-by-ten '(1 2 3))
;; => (11 12 13)
\end{verbatim}

A \texttt{lambda} inside of a named function may also be used to do something over and over again, with the help of \texttt{let}. You may, for instance, have a function that needs to greet a list of people as a side effect with \texttt{mapc} and you do not want to define the same function more than once:

\begin{verbatim}
(defun my-greet-teams (&rest teams)
  "Say hello to each person in TEAMS and return list with all persons per team.
Each member of TEAMS is a list of strings."
  (let* ((greet-name (lambda (name)
                       (message "Hello %s" name)))
         (greet-team-and-names (lambda (team)
                                 (message "Greeting the team of `%s'..." team)
                                 (mapc greet-name team))))
    (mapcar greet-team-and-names teams)))

(my-greet-teams
 '("Pelé" "Ronaldo")
 '("Maradona" "Messi")
 '("Beckenbauer" "Neuer")
 '("Platini" "Zidane")
 '("Baresi" "Maldini")
 '("Eusebio" "Cristiano Ronaldo")
 '("Xavi" "Iniesta")
 '("Charlton" "Shearer")
 '("Puskas" "Kubala")
 '("All of the Greece Euro 2004 squad ;)"))
;; => (("Pelé" "Ronaldo") ("Maradona" "Messi") ...)
\end{verbatim}

The greetings are a side effect in this case and are available in the \texttt{*Messages*} buffer. You can quickly access that buffer with C-h e (\texttt{view-echo-area-messages}). It does not really matter what \texttt{my-greet-teams} is doing. Focus on the combination of a named function and anonymous functions inside of it.
\section{Make your interactive function also work from Lisp calls}
\label{sec:org10592f7}
Functions can be used interactively when they are declared with the \texttt{interactive} specification. This turns them into ``commands''. They can be called via their name by first doing M-x (\texttt{execute-extended-command}) and then finding the command. They may also be assigned to a key and invoked directly by pressing that key. In its simplest form, the \texttt{interactive} specification is an unquoted list like \texttt{(interactive)}. Here is a trivial example that calls \texttt{read-string} to produce a minibuffer prompt which accepts user input and returns it as a string:

\begin{verbatim}
(defun my-greet-person ()
  (interactive)
  (message "Hello %s" (read-string "Whom to greet? ")))
\end{verbatim}

The problem with the above implementation is that it is only useful in interactive use. If you want to issue such a greeting non-interactively through a program, you need to write another function that does practically the same thing except that it takes a \texttt{NAME} argument. Like this:

\begin{verbatim}
(defun my-greet-person-with-name (name)
  "Greet person with NAME."
  (message "Hello %s" name))
\end{verbatim}

You do not need to write two separate functions which practically do the same thing. Instead, you can have one function, with its parameters, which decides how to get the values of the arguments passed to it depending on if it is called interactively or programmatically. Consider this scenario:

\begin{verbatim}
(defun my-greet-interactive-and-non-interactive (name)
  "Greet person with NAME.
When called interactively, produce a minibuffer prompt asking for NAME.

When called from Lisp, NAME is a string."
  (interactive (list (read-string "Whom to greet? ")))
  (message "Hello %s" name))
\end{verbatim}

The documentation I wrote there tells you exactly what is happening. Though let me explain \texttt{interactive} in further detail: it takes an argument, which is a list that corresponds to the argument list of the current \texttt{defun}. In this case, the \texttt{defun} has a list of arguments that includes a single element, the \texttt{NAME}. Thus, \texttt{interactive} also has a list with one element, whose value corresponds to \texttt{NAME}. If the parameters were more than one, then the \texttt{interactive} would have to be written accordingly: each of its elements would correspond to the parameter at the same index on the list.

This list of expressions you pass to \texttt{interactive} essentially is the preparatory work that binds values to the parameters. When you call the above function interactively, you practically tell Emacs that in this case \texttt{NAME} is the return value of the call to \texttt{read-string}. For more parameters, you get the same principle but I write it down just to be clear:

\begin{verbatim}
(defun my-greet-with-two-parameters (name country)
  "Greet person with NAME from COUNTRY.
When called interactively, produce a minibuffer prompt asking for NAME
and then another prompt for COUNTRY.

When called from Lisp, NAME and COUNTRY are strings."
  (interactive
   (list
    (read-string "Whom to greet? ")
    (read-string "Where from? ")))
  (message "Hello %s of %s" name country))

(my-greet-with-two-parameters "Protesilaos" "Cyprus")
;; => "Hello Protesilaos of Cyprus"
\end{verbatim}

Write \texttt{interactive} specifications with care and you will end up with a rich corpus of code that is economical and flexible.
\section{COPYING}
\label{sec:orgf1e6bff}
Copyright (C) 2025 Protesilaos Stavrou

\begin{quote}
Permission is granted to copy, distribute and/or modify this document
under the terms of the GNU Free Documentation License, Version 1.3 or
any later version published by the Free Software Foundation; with no
Invariant Sections, with the Front-Cover Texts being “A GNU Manual,” and
with the Back-Cover Texts as in (a) below.  A copy of the license is
included in the section entitled “GNU Free Documentation License.”

(a) The FSF’s Back-Cover Text is: “You have the freedom to copy and
modify this GNU manual.”
\end{quote}
\section{GNU Free Documentation License}
\label{sec:org476d4b3}
\section{Indices}
\label{sec:orgad3448b}
\subsection{Function index}
\label{sec:orgd40472b}
\subsection{Variable index}
\label{sec:org9f0acee}
\subsection{Concept index}
\label{sec:orgf087f0c}
\end{document}
